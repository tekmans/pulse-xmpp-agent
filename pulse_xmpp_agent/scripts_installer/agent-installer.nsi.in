;
; (c) 2016 siveo, http://www.siveo.net
;
; This file is part of Pulse 2, http://www.siveo.net
;
; Pulse 2 is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; Pulse 2 is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Pulse 2; if not, write to the Free Software
; Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston,
; MA 02110-1301, USA.


; Make sure the installer runs as admin
RequestExecutionLevel admin

; Define a few variables
!define PRODUCT_NAME "Pulse Agent"
!define PRODUCT_PUBLISHER "SIVEO"
!define PRODUCT_WEB_SITE "http://www.siveo.net"
!define PRODUCT_VERSION "@@PRODUCT_VERSION@@"
!define PRODUCT_DIR_REGKEY "Software\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; Variables replaced by the script calling the nsi
!define DOWNLOADS_DIR "@@DOWNLOADS_DIR@@"
!define PYTHON_MSI "@@PYTHON_MSI@@"
!define PY_VCPYTHON27 "@@PY_VCPYTHON27@@"
!define PY_WIN32 "@@PY_WIN32@@"
!define PY_PIP "@@PY_PIP@@"
!define PY_NETIFACES "@@PY_NETIFACES@@"
!define PY_COMTYPES "@@PY_COMTYPES@@"
!define PY_CONFIGPARSER "@@PY_CONFIGPARSER@@"
!define PY_UTILS "@@PY_UTILS@@"
!define PY_SLEEKXMPP "@@PY_SLEEKXMPP@@"
!define PY_WMI "@@PY_WMI@@"
!define PULSE_AGENT "@@PULSE_AGENT@@"
!define PULSE_AGENT_CONFFILE "@@PULSE_AGENT_CONFFILE@@"
!define PULSE_AGENT_NAME "@@PULSE_AGENT_NAME@@"
!define PULSE_AGENT_MODULE "@@PULSE_AGENT_MODULE@@"
!define PULSE_AGENT_TASK_XML "@@PULSE_AGENT_TASK_XML@@"

SetCompressor lzma

; Modern UI installer stuff
!include "MUI2.nsh"
!define MUI_ABORTWARNING
!define MUI_ICON "artwork/install.ico"
!define MUI_WELCOMEPAGE_TITLE_3LINES
#!define MUI_HEADERIMAGE
#!define MUI_HEADERIMAGE_RIGHT
#!define MUI_HEADERIMAGE_BITMAP "artwork/header.bmp"
#!define MUI_WELCOMEFINISHPAGE_BITMAP "artwork/wizard.bmp"

; UI pages
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_LANGUAGE "English"

; Other useful modules
!include "WinVer.nsh"
!include "FileFunc.nsh"

; Logs macros
var hFileLog
!macro Log_Init logfile
FileOpen $hFileLog "${logfile}" w
!macroend
!macro Log_String msg
DetailPrint "${msg}"
FileWrite $hFileLog "${msg}$\r$\n"
!macroend
!macro Log_Close
FileWrite $hFileLog 'Done.'
FileClose $hFileLog
!macroend

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Pulse-Agent-win32-${PRODUCT_VERSION}.exe"
InstallDir "$PROGRAMFILES\Pulse"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show

; Define a few settings
Section -SETTINGS
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
SectionEnd

; Python installation
Section "Python" sec_py
  SetOutPath "$INSTDIR\tmp"
  File "${DOWNLOADS_DIR}/${PYTHON_MSI}"
  File "${DOWNLOADS_DIR}/${PY_VCPYTHON27}"
  !insertmacro Log_String "Python Installation...."
  !insertmacro Log_String "------------------------------------------------------"

  ; Install of Python
  StrCpy $0 `msiexec /i "$INSTDIR\tmp\${PYTHON_MSI}" /quiet /norestart ALLUSERS=1`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Install of Visual C++ Compiler for Python
  StrCpy $0 `msiexec /i "$INSTDIR\tmp\${PY_VCPYTHON27}" /quiet /norestart ALLUSERS=1`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  Delete $INSTDIR\tmp\${PYTHON_MSI}
  Delete $INSTDIR\tmp\${PY_VCPYTHON27}
SectionEnd

; Installation of Python modules needed by Pulse Agent
Section "Python Modules" sec_pymod
  SetOutPath "$INSTDIR\tmp"
  File "${DOWNLOADS_DIR}/${PY_WIN32}"
  File "${DOWNLOADS_DIR}/${PY_PIP}"
  File "${DOWNLOADS_DIR}/${PY_NETIFACES}"
  File "${DOWNLOADS_DIR}/${PY_COMTYPES}"
  File "${DOWNLOADS_DIR}/${PY_CONFIGPARSER}"
  File "${DOWNLOADS_DIR}/${PY_UTILS}"
  File "${DOWNLOADS_DIR}/${PY_SLEEKXMPP}"
  File "${DOWNLOADS_DIR}/${PY_WMI}"
  !insertmacro Log_String "Python modules Installation...."
  !insertmacro Log_String "------------------------------------------------------"

  ; Install of Python module for Windows Extensions (pywin32)
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --no-index --find-links="$INSTDIR\tmp" ${PY_WIN32}`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Install of Python pip module
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --no-index --find-links="$INSTDIR\tmp" pip`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Install of Python netifaces module
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --no-index --find-links="$INSTDIR\tmp" netifaces`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Install of Python comtypes module
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --no-index --find-links="$INSTDIR\tmp" comtypes`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Install of Python configparser module
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --pre --no-index --find-links="$INSTDIR\tmp" configparser`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Install of Python utils module
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --no-index --find-links="$INSTDIR\tmp" utils`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Install of Python sleekxmpp module
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --no-index --find-links="$INSTDIR\tmp" sleekxmpp`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Install of Python wmi module
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --no-index --find-links="$INSTDIR\tmp" wmi`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  Delete $INSTDIR\tmp\${PY_WIN32}
  Delete $INSTDIR\tmp\${PY_PIP}
  Delete $INSTDIR\tmp\${PY_NETIFACES}
  Delete $INSTDIR\tmp\${PY_COMTYPES}
  Delete $INSTDIR\tmp\${PY_CONFIGPARSER}
  Delete $INSTDIR\tmp\${PY_UTILS}
  Delete $INSTDIR\tmp\${PY_SLEEKXMPP}
  Delete $INSTDIR\tmp\${PY_WMI}
SectionEnd

; Installation of Pulse Agent
Section "!${PRODUCT_NAME}" sec_app
  SetOutPath "$INSTDIR\tmp"
  File "${PULSE_AGENT}"
  !insertmacro Log_String "Pulse Agent Installation...."
  !insertmacro Log_String "------------------------------------------------------"

  ; Install of Pulse agent
  StrCpy $0 `C:\Python27\Scripts\pip install --upgrade --no-index --find-links="$INSTDIR\tmp" ${PULSE_AGENT}`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Copy of agent config file
  SetOutPath "$INSTDIR\etc"
  File "../config/${PULSE_AGENT_CONFFILE}"

  ; Creation of the task
  SetOutPath "$INSTDIR\tmp"
  File "${PULSE_AGENT_TASK_XML}"
  ${If} ${AtLeastWin7}
    StrCpy $0 `SCHTASKS /Create /XML "$INSTDIR\tmp\${PULSE_AGENT_TASK_XML}" /TN "${PRODUCT_NAME}"`
  ${Else}
    StrCpy $0 `SCHTASKS /Create /SC ONSTART /RU "System" /TN "${PRODUCT_NAME}" /TR "C:\Python27\python.exe C:\Python27\Lib\site-packages\${PULSE_AGENT_MODULE}\launcher.py -t machine"`
  ${EndIf}
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Create log folder to hold agent logs
  CreateDirectory $INSTDIR\var\log

  ; Run the agent
  !insertmacro Log_String "Running agent"
  StrCpy $0 `"C:\Python27\python.exe" C:\Python27\Lib\site-packages\${PULSE_AGENT_MODULE}\launcher.py -t machine`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  SectionIn RO
  WriteUninstaller $INSTDIR\uninstall.exe
  ; Add ourselves to Add/remove programs
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "${PRODUCT_NAME}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "InstallLocation" "$INSTDIR"
  WriteRegDWORD ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "NoModify" 1
  WriteRegDWORD ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "NoRepair" 1

  ; Write the version installed in registry
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "CurrentVersion" "${PRODUCT_VERSION}"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "InstallLocation" "$INSTDIR"

  Delete $INSTDIR\tmp\${PULSE_AGENT}
  Delete $INSTDIR\tmp\${PULSE_AGENT_TASK_XML}
SectionEnd

; Setup of RDP & Firewall
Section "RDP Setup" sec_rdp
  !insertmacro Log_String "RDP setup for remote control of machine via Pulse...."
  !insertmacro Log_String "------------------------------------------------------"

  ; Setup of rdp
  StrCpy $0 `reg add "HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Terminal Server" /v fDenyTSConnections /t REG_DWORD /d 0 /f`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"
  ${If} ${AtLeastWin7}
    StrCpy $0 `reg add "HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Terminal Server\WinStations\RDP-Tcp" /v UserAuthentication /t REG_DWORD /d 0 /f`
  ${EndIf}
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"
  ${If} ${AtLeastWin7}
    StrCpy $0 `reg add "HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Terminal Server\WinStations\RDP-Tcp" /v SecurityLayer /t REG_DWORD /d 0 /f`
  ${EndIf}
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"

  ; Setup of Windows firewall to allow rdp
  ${If} ${IsWinXP}
    StrCpy $0 `netsh firewall set service type = remotedesktop mode = enable`
  ${Else}
    StrCpy $0 `netsh advfirewall firewall set rule group="remote desktop" new enable=Yes`
  ${EndIf}
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  !insertmacro Log_String "-------------------"
SectionEnd

; What needs to be done for uninstalling
Section "Uninstall"
  Delete $INSTDIR\uninstall.exe
  StrCpy $0 `SCHTASKS /Delete /TN "Pulse Agent"`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  StrCpy $0 `C:\Python27\Scripts\pip uninstall ${PULSE_AGENT_MODULE} -y`
  !insertmacro Log_String "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  !insertmacro Log_String "Return code was: $1"
  Delete $INSTDIR\etc\${PULSE_AGENT_CONFFILE}
  RMDir /r $INSTDIR
  DeleteRegKey HKLM "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
SectionEnd

; Functions

Function .onInit
  ; XP or later
  ${IfNot} ${AtLeastWinXP}
    MessageBox MB_OK|MB_ICONEXCLAMATION "XP and above required for running ${PRODUCT_NAME}"
    Quit
  ${EndIf}

  ; Install log initialization
  CreateDirectory "$INSTDIR\tmp"
  !insertmacro Log_Init "$INSTDIR\tmp\install.log"
  !insertmacro Log_String "Starting install..."
  !insertmacro GetTime
  ${GetTime} "" "L" $0 $1 $2 $3 $4 $5 $6
  !insertmacro Log_String "Start time: $3 $0/$1/$2 at $4:$5:$6"
FunctionEnd

Function .onGUIEnd
  ; Write the log file
  !insertmacro Log_Close
FunctionEnd

Function .onMouseOverSection
  ; Find which section the mouse is over, and set the corresponding description.
  FindWindow $R0 "#32770" "" $HWNDPARENT
  GetDlgItem $R0 $R0 1043 ; description item (must be added to the UI)

  StrCmp $0 ${sec_py} 0 +2
    SendMessage $R0 ${WM_SETTEXT} 0 "STR:The Python interpreter. \
          This is required for ${PRODUCT_NAME} to run."

  StrCmp $0 ${sec_pymod} 0 +2
    SendMessage $R0 ${WM_SETTEXT} 0 "STR:Python modules required by ${PRODUCT_NAME}."

  StrCmp $0 ${sec_app} "" +2
    SendMessage $R0 ${WM_SETTEXT} 0 "STR:${PRODUCT_NAME}"

  StrCmp $0 ${sec_rdp} "" +2
    SendMessage $R0 ${WM_SETTEXT} 0 "STR:Setup RDP protocol required for remote control"
FunctionEnd
